#                                                                                                                       
# ~/.zshrc
#

# Key bindings for Ctrl + Left and Ctrl + Right
bindkey '^[[1;5C' forward-word  # Ctrl + Right
bindkey '^[[1;5D' backward-word # Ctrl + Left

# Infinite history settings
HISTFILE=~/.zsh_history       # Location of the history file
HISTSIZE=1000000000           # Unlimited in-memory history size
SAVEHIST=1000000000           # Unlimited on-disk history size
setopt append_history         # Append history to file (don't overwrite)
setopt inc_append_history     # Write to history immediately
setopt share_history          # Share history across multiple sessions

# Source aliases from .shell_aliases
if [ -f ~/.shell_aliases ]; then
  source ~/.shell_aliases
fi

autoload -Uz add-zsh-hook

zle-line-init() {
    emulate -L zsh

    [[ $CONTEXT == start ]] || return 0

    while true; do
      zle .recursive-edit
      local -i ret=$?
      [[ $ret == 0 && $KEYS == $'\4' ]] || break
      [[ -o ignore_eof ]] || exit 0
    done

    local saved_prompt=$PROMPT
    local saved_rprompt=$RPROMPT

    PROMPT=$'\e[93m‚ùØ \e[0m\e[93m'
    RPROMPT=$'\e[0m'

    zle .reset-prompt

    PROMPT=$saved_prompt
    RPROMPT=$saved_rprompt

    if (( ret )); then
      zle .send-break
    else
      zle .accept-line
    fi
    return ret
}

fix-prompt-bottom() {
  local term_lines=$(tput lines)  # Total number of terminal lines

  # Move the cursor to last line
  print -n "\e[${term_lines};H"

  # Ensures that ZLE is active before redrawing the prompt
  [[ -n $ZLE ]] && zle reset-prompt
}

zle -N zle-line-init
add-zsh-hook precmd fix-prompt-bottom

# Shell prompt
eval "$(starship init zsh)"
source /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
